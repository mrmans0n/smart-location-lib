buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'


version '3.2.6'
group 'io.nlopez.smartlocation'

dependencies {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }
    compile 'com.google.android.gms:play-services-location:9.4.0'
    compile 'com.android.support:support-annotations:23.4.0'

    provided 'io.reactivex:rxjava:1.1.7'
    provided 'io.reactivex:rxandroid:1.2.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.1'
    testCompile 'org.robolectric:robolectric-shadows:3.1'
    testCompile 'org.mockito:mockito-core:2.0.86-beta'
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    buildTypes {
        defaultConfig {
            minSdkVersion 9
            targetSdkVersion 23
            versionCode 20005
            versionName version
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

}

android.libraryVariants
publishing {
    publications {
        maven(MavenPublication) {
            artifact "build/libs/SmartLocation-${version}.aar"
            artifactId 'library'
        }
    }
}

def isReleaseBuild() {
    return version.contains("SNAPSHOT") == false
}

def getProperty(project, property) {
    return project.hasProperty(property) ? project.property(property) : ""
}

signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: getProperty(project, "sonatypeRepo")) {
            authentication(userName: getProperty(project, "sonatypeUsername"),
                    password: getProperty(project, "sonatypePassword"))
        }
        snapshotRepository(url: getProperty(project, "sonatypeSnapshotRepo")) {
            authentication(userName: getProperty(project, "sonatypeUsername"),
                    password: getProperty(project, "sonatypePassword"))
        }

        pom.project {
            name 'Smart Location Library'
            packaging 'aar'
            description 'This library handles all the boilerplate and repetitive code used when playing with Locations in Android projects.'
            url 'https://github.com/mrmans0n/smart-location-lib'
            artifactId 'library'

            scm {
                url 'scm:git@github.com:mrmans0n/smart-location-lib.git'
                connection 'scm:git@github.com:mrmans0n/smart-location-lib.git'
                developerConnection 'scm:git@github.com:mrmans0n/smart-location-lib.git'
            }

            licenses {
                license {
                    name 'MIT license'
                    url 'http://www.opensource.org/licenses/mit-license.php'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'mrmans0n'
                    name 'Nacho Lopez'
                    email 'nacho@nlopez.io'
                }
            }
        }
    }
}
